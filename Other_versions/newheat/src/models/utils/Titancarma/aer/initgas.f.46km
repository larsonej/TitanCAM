       subroutine initgas
c
c
c  @(#) initgas.f  Ackerman  Dec-1995
c  This routine initializes the atmospheric profiles of all gases.
c
c    gc       Gas concentration at layer mid-point [g/cm^3]
c
c  Presently the only vertical coordinate is altitude, zl [cm].
c
c
c  Argument list input:
c    None.
c
c  Argument list output:
c    None.
c
c
c  Include global constants and variables
c
      include 'globaer.h'
c
c
c  Declare local variables
c
      character*(25) gasfile
c
c
c   Define formats
c
    1 format(/,'Gas concentrations for ',a,'(initgas)',//,
     $  a3, 1x, 4(a11,4x), /) 
    2 format(i3,1x,1p,3(e11.3,4x),0p,f11.3)
    3 format(i3,2(1pe13.6))
   22 format(4(1pe12.2,3x))

c
c-------------------------------------------------------------------------------
c
c  Announce entry to this routine
c
      if( DEBUG ) write(LUNOPRT,'(/,a)') 'Enter initgas'
c
c-------------------------------------------------------------------------------
c
c
c   Calculate vapor pressures
c
      do ixyz = 1,NXYZ
        call vaporp
      enddo
c
c
c   Parameters for ethane profile
c
      igas = 1
c
c
c   <rh_init> is initial relative humidity [%]
c
      rh_init = 40.0d0
      rhi_init = 60.0d0
      rh_init = 100.0d0
      rhi_init = 100.0d0
      rh_init = 80.0d0
c
c
c   Gas constant for ethane
c
      rvap = RGAS/gwtmol(igas)
c
c
c   Loop over all spatial dimensions and gases
c
c
c  Set initial gas concentration from file# cf368 - which is at steady-state
c  down to ~20 km
c
      do igas = 1,NGAS
        if( igas .eq. 1 )then

c
c  ..gas file
c
        gasfile='Files/Output/gascf368.p'
c
        open(unit=4,file=gasfile,status='old')
          do k = 1,NZ
            read(4,22) rjunk,gc(1,1,k,1),gcmix,rhc2h6i
            supsati3(k,1) = rhc2h6i/100. - ONE
          enddo
          read(4,22) rjunk,gc46,gcmix,rhc2h6i
          close(unit=4)
c
c
        else
          write(LUNOPRT,'(/,a)') 'invalid <igas> in initgas.f'
          stop 1
        endif

      enddo
c
c
c  Specify fluxes at top and bottom of model [g/cm^2/s]
c
      do igas = 1,NGAS
        do ixy = 1,NXY
          ftopgas(ixy,igas) = 4.1777760091208E-09
          fbotgas(ixy,igas) = 0.
        enddo
      enddo

c     open(unit=2,file='../data/flux46_gas',status='old')
c     read(2,3) ijunk,difflu,rjunk
c     read(2,3) ijunk,rjunk,diffld
c     ftopgas(1,1) = diffld*gc46 - difflu*gc3(NZ,1)
c     close(unit=2)

c
c
c  Scale particle concentrations and boundary fluxes from 
c  cartesian coordinates to coordinate system specified by <igrid>
c
c
c  Pick indices for top and bottom layers
c
      if( igridv .eq. I_CART )then
        iztop = NZ
        izbot = 1
      else
        iztop = 1
        izbot = NZ
      endif

      do igas = 1, NGAS

        do ixyz = 1,NXYZ
          gc3(ixyz,igas) = gc3(ixyz,igas) * 
     $                     ( xmet3(ixyz)*ymet3(ixyz)*zmet3(ixyz) )
        enddo

        do ixy = 1,NXY
          ftopgas(ixy,igas) = ftopgas(ixy,igas) *
     $                        ( xmet2(ixy,iztop)*ymet2(ixy,iztop) )
          fbotgas(ixy,igas) = fbotgas(ixy,igas) *
     $                        ( xmet2(ixy,izbot)*ymet2(ixy,izbot) )
        enddo
      enddo
c
c
c  Initialize <supsati> and <supsatl>
c
      do ixyz = 1,NXYZ
        call supersat
      enddo
c
c
c  Print gas concentrations at 1 horizontal grid point
c
      ix = 1
      iy = 1
      ixy = NX * ( iy - 1 ) + ix 
c
c
c  Set vertical loop index to increment downwards
c
      if( igridv .eq. I_CART )then
        kb  = NZ
        ke  = 1
        idk = -1
      else 
        kb  = 1
        ke  = NZ
        idk = 1
      endif

      do igas = 1,NGAS

        write(LUNOPRT,1) gasname(igas),
     $                   'iz','zc','gc [g/cm^3]','supsat','T [K]'

        do iz = kb,ke,idk
          xyzmet = xmet2(ixy,iz)*ymet2(ixy,iz)*zmet2(ixy,iz)
          write(LUNOPRT,2) iz, zc2(ixy,iz), gc2(ixy,iz,igas)/xyzmet,
     $       supsatl2(ixy,iz,igas), t2(ixy,iz)
        enddo

      enddo
c
c  Return to caller with gas concentrations initialized.
c
      return
      end
